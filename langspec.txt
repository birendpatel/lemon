# Language Specification for the Lemon programming Language
# Copyright (C) 2021 Biren Patel
# GNU General Public License v3.0

#-------------------------------------------------------------------------------
# Notation
#-------------------------------------------------------------------------------

The metasyntax for the Lemon grammar is specified via extended Backus-Naur form.
Some standard EBNF notation has been replaced with regex-like equivalents for
readability, familiarity, and simplicity.

nonterminal		<foo>
terminal		"foo"
lexical element		FOO
rule termination	\n\n
definition		::=
empty string		Îµ

Metasyntax operators in order of decreasing precedence:

repeat zero or more	*
repeat one or more	+
optional		?
grouping		()
alternation		|

#-------------------------------------------------------------------------------
# Lemon operator precedence
# Precedence rules for priorities 1 and 2 are identical to C, and the rules for
# priorities 3 - 9 are identical to Go.
#-------------------------------------------------------------------------------

Priority	Operators	Grouping	Comment
--------	---------	--------	-------
1		()		LR		Function call, grouping
-		[]		LR		Composite types indexing/decls
-		.		LR		Method and attribute access
2		!		RL		Logical NOT
-		'		RL		Bitwise NOT
-		+		RL		Unary positive
-		-		RL		Unary minus
-		*		RL		Indirection
-		&		RL		Address of
-		(foo)		RL		Type casting
3		*		LR		Multiplication
-		/		LR		Division
-		%		LR		Mod
-		>>		LR		Right shift
-		<<		LR		Left shift
-		&		LR		Bitwise AND
4		+		LR		Binary addition
-		-		LR		Binary subtraction
-		|		LR		Bitwise OR
-		^		LR		Bitwise XOR
6		>		LR		Greater than
-		<		LR		Less than
-		>=		LR		Greater than or equal to
-		<=		LR		Less than or equal to
-		==		LR		Logical equals
-		!=		LR		Logical not equals
7		&&		LR		Logical AND
8		||		LR		Logical OR
9		=		RL		Assignment

#-------------------------------------------------------------------------------
# Lexical Grammar
#-------------------------------------------------------------------------------

IDENTIFIER ::= [a-zA-Z_][a-zA-Z_0-9]*

LITERAL ::= INT_LITERAL
	  | FLOAT_LITERAL
	  | STRING_LITERAL

#-------------------------------------------------------------------------------
# Syntactical Grammar
#-------------------------------------------------------------------------------

<file> ::= <declaration>* "EOF" ;

#-------------------------------------------------------------------------------
# Lemon does not require a main() function as its entry point. Therefore,
# statements may exist at the top level alongside declarations.
#-------------------------------------------------------------------------------

<declaration> ::= <struct declaration>
		| <function declaration>
		| <variable declaration>
		| <statement>

#-------------------------------------------------------------------------------
# A struct declaration just introduces a struct as a new type, and it can be
# used in other declarations via its tag identifier. Unlike other C-esque
# languages, the predeclared types like i32, f32, etc. cannot be type alised.
#-------------------------------------------------------------------------------

<struct declaration> ::= "struct" IDENTIFIER "{" <member list> "}" ";"

<member list> ::= (("pub" | "priv") "mut"? IDENTIFIER ":" <type>";")+

#-------------------------------------------------------------------------------
# Functions may be methods taking a pointer to the receiver or the receiver
# itself.
#
# example: func priv foo(x: i32, y: f32) return void for *bar {...} is a private
# method named foo with two parameters and returning nothing.
#-------------------------------------------------------------------------------

<function declaration> ::= 
	"func" ("priv" | "pub") IDENTIFIER "(" ("void" | <parameter list>) ")"
	"return" ("void" | <type>) ("for" <type>)? <block statement>

<parameter list> ::= 
	"mut"? IDENTIFIER ":" <type> ("," "mut"? IDENTIFIER ":" <type>)*

#-------------------------------------------------------------------------------
# variable declarations default to the zero-value of the type if no explicit
# initializer is given. The optional mut terminal allows the variable to
# be modified after its declaration. Variables in Lemon are default immutable
# to take advantage of optimisations.
#
# Lemon does not allow multiple declarations such as "let x, y: int = 0, 1;".
#-------------------------------------------------------------------------------

<variable declaration> ::= 
	"let" "mut"? IDENTIFIER ":" <type> "=" <expression> ";"

<short declaration> ::= IDENTIFIER : <type> "=" <expression>

#-------------------------------------------------------------------------------
# types are identifiers perhaps prefixed with leading symbols which construct
# a series of arrays and pointers, or perhaps a combination of the two.
#-------------------------------------------------------------------------------

<type> ::= IDENTIFIER | <composite type>

<composite type> ::= <pointer type> | <array type>

<pointer type> ::= "*" <type>

<array type> ::= "[" INT_LITERAL "]" <type>

#-------------------------------------------------------------------------------
# Statements can be direct children of file nodes. Lemon acts like a dynamic
# language in the sense that it does not need a main function as an entry point.
# Statments are executed procedurally.
#-------------------------------------------------------------------------------

<statement> ::= <expression statement>
	      | <block statement>
	      | <for statement>
	      | <while statement>
	      | <switch statement>
	      | <branch statement>
	      | <return statement>

<expression statement> ::= <expression> ";"

<return statement> ::= "return" <expression> ";"

#-------------------------------------------------------------------------------
# Block statements may include any declarations. Function declarations in a
# block may be used to create closures.
#-------------------------------------------------------------------------------

<block statement> ::= "{" <declaration>* "}"

#-------------------------------------------------------------------------------
# Unlike C, loop control flow does not have optional controlling expressions.
# Loops also require block statement.
#
# Both for loops and branch statements can possess a short declaration. These
# are mutable variables. In branch statements the short declaration is entirely
# optional. Where present, it can be referenced in both the if and else clauses.
#-------------------------------------------------------------------------------

<for statement> ::= 
	"for" "(" (<short declaration> | <expr>) ";" <expr> ";" <expr> ";" ")"
	<block statement>

<while statement> ::= "while" "(" <expr> ")" <block statement>

<branch statement> ::= 
	"if" "(" <short declaration>? <expr> ")" <block statement>
	("else" (<branch statement> | <block statement>))?

#-------------------------------------------------------------------------------
# The expression grammar is stratified according to the operator precedence
# rules. Access rules are placed above the atom rule to avoid left recursion,
# i.e., <atom> ::= <atom> <call> | <atom> <index> ...
#
# Note: assignment LHS is checked during context sentitive analysis.
#-------------------------------------------------------------------------------

<expression> ::= <assignment>

<assignment> ::= <logical or> ("=" <assignment>)?

<logical or> ::= <logical and> ("||" <logical and>)*

<logical and> ::= <equality> ("&&" <equality>)*

<equality> ::= <term> ((">" | "<" | ">=" | "<=" | "==" | "!=") <term>)*

<term> ::= <factor> (("+" | "-" | "|" | "^") <factor>)*

<factor> ::= <unary> (("*" | "/" | "%" | ">>" | "<<" | "&") <unary>)*

<unary> ::= ("-" | "+" | "'" | "!" | "*" | "&" | <cast>) <unary> | <primary>

<cast> ::= "(" IDENTIFIER ")"

<primary> ::= <atom> (<call> | <attribute> | <index>)*

<call> ::= "(" <arguments>? ")"

<attribute> ::= "." IDENTIFIER

<index> ::= "[" <expression> "]"

<arguments> ::= <expression> ("," <expression>)*

<atom> ::= "self"
	 | "true"
	 | "false
	 | "null"
	 | <literal>
	 | IDENTIFIER
	 | <group>

<group> ::= "(" <expression> ")"

#-------------------------------------------------------------------------------
# Literal rules. The Lemon scanner is unable to handle detection of compound
# literals due to indirect recursion.
#-------------------------------------------------------------------------------

<literal> ::= LITERAL | <atomic literal> | <compound literal>

<atomic literal> ::= "true" | "false" | "null"

<compound literal> ::= <array literal> | <rvar literal>

<array literal> ::= "[" <tagged index> ("," <tagged index>)* "]"

<tagged index> ::= ("[" INT_LITERAL "]" "=")? <literal>

<rvar literal> ::= IDENTIFIER "~" "(" <rvar parameter list> ")"

<rvar parameter list> ::= INT_LITERAL | FLOAT_LITERAL
			  ("," (INT_LITERAL | FLOAT_LITERAL))*


